apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'de.mannodermaus.android-junit5'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'dagger.hilt.android.plugin'

def gitTag = 'git describe --tags --abbrev=0'.execute([], project.rootDir)
        .text.trim().replace("v", "")
def gitCommitCount = Integer.parseInt('git rev-list --count HEAD'
        .execute([], project.rootDir).text.trim())

ktlint {
    android = true
    disabledRules = ['import-ordering']
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    buildFeatures {
        viewBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    defaultConfig {
        applicationId "xyz.mcmxciv.halauncher"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode gitCommitCount
        versionName gitTag
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "PRIVACY_POLICY_URL", '"https://halauncher.app/privacy"')

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
                arg("room.incremental", "true")
            }
        }
    }

    signingConfigs {
        release {
            storeFile file('releasekey.jks')
            storePassword "${System.env.KEYSTORE_PASSWORD ?: ""}"
            keyAlias "$System.env.KEYSTORE_ALIAS" ?: ""
            keyPassword "${System.env.KEYSTORE_ALIAS_PASSWORD ?: ""}"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
        }
    }

    lintOptions {
        warning 'InvalidPackage'
    }

    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
        }
        unitTests.all {
            testLogging.events = ['passed', 'skipped', 'failed']
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.72'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.work:work-runtime-ktx:2.3.4'

    implementation 'androidx.room:room-runtime:2.2.5'
    implementation 'androidx.room:room-ktx:2.2.5'
    kapt 'androidx.room:room-compiler:2.2.5'

    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'

    implementation 'com.google.dagger:dagger:2.28.1'
    kapt 'com.google.dagger:dagger-compiler:2.28.1'
    implementation 'com.google.dagger:hilt-android:2.28.1-alpha'
    kapt 'com.google.dagger:hilt-android-compiler:2.28.1-alpha'

    implementation 'androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha01'
    kapt 'androidx.hilt:hilt-compiler:1.0.0-alpha01'

    implementation 'androidx.hilt:hilt-work:1.0.0-alpha01'
    kapt 'androidx.hilt:hilt-compiler:1.0.0-alpha01'

    implementation 'com.squareup.moshi:moshi-kotlin:1.9.3'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.9.3'

    implementation 'com.squareup.okhttp3:okhttp:4.7.2'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'

    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'com.google.android.play:core:1.7.3'
    implementation 'com.google.firebase:firebase-analytics:17.4.3'
    implementation 'com.google.firebase:firebase-crashlytics:17.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.3.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta7'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.0'
    implementation 'com.github.hadilq.liveevent:liveevent:1.2.0'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'com.github.alexjlockwood:kyrie:0.2.1'

    testImplementation 'junit:junit:4.13'
    testImplementation 'io.mockk:mockk:1.10.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.7.2'
    testImplementation 'org.spekframework.spek2:spek-dsl-jvm:2.0.8'
    testImplementation 'org.spekframework.spek2:spek-runner-junit5:2.0.8'
    testImplementation 'org.jetbrains.kotlin:kotlin-reflect:1.3.72'
    testImplementation 'org.assertj:assertj-core:3.16.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
